plugins {
    id 'java-library'
    id 'maven-publish'
    id("org.jreleaser") version "1.21.0"
}

group 'org.kinotic'
version '5.1.0'

sourceCompatibility = '21'

repositories {
    mavenCentral()
}

ext {
    apacheCommonsVersion='3.20.0'
    logbackVersion='1.5.25'
    nettyVersion='4.1.115.Final'
    vertxVersion='5.0.7'
}


dependencies {
    implementation "io.vertx:vertx-core:${vertxVersion}"
    implementation "io.vertx:vertx-web:${vertxVersion}"

    // If needed we can override netty to use the latest version, allowing us to patch security issues
//    implementation "io.netty:netty-buffer:${nettyVersion}"
//    implementation "io.netty:netty-codec-http2:${nettyVersion}"
//    implementation "io.netty:netty-codec-http:${nettyVersion}"
//    implementation "io.netty:netty-common:${nettyVersion}"
//    implementation "io.netty:netty-handler-proxy:${nettyVersion}"
//    implementation "io.netty:netty-handler:${nettyVersion}"
//    implementation "io.netty:netty-resolver-dns:${nettyVersion}"
//    implementation "io.netty:netty-resolver:${nettyVersion}"
//    implementation "io.netty:netty-transport:${nettyVersion}"

    implementation "ch.qos.logback:logback-core:${logbackVersion}"
    implementation "ch.qos.logback:logback-classic:${logbackVersion}"

    implementation "org.apache.commons:commons-lang3:${apacheCommonsVersion}"

    testImplementation group: 'junit', name: 'junit', version: '4.13.2'
}

java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    publications {
        mavenJava(MavenPublication) {

            from components.java

            // Use resolved versions since we are not directly depending on the spring POM
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }

            pom {
                name = 'Vert.x STOMP Server'
                description = 'A simplified STOMP server for use with Vert.x'
                url = 'https://github.com/MindIgnited/vertx-stomp-lite'

                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        name = 'Mind Ignited'
                        email = 'develop@mindignited.com'
                    }
                }
                scm {
                    connection = 'scm:git:https://github.com/MindIgnited/vertx-stomp-lite.git'
                    developerConnection = 'scm:git:https://github.com/MindIgnited/vertx-stomp-lite.git'
                    url = 'https://github.com/MindIgnited/vertx-stomp-lite'
                }
            }
        }
    }
    // Here is where JReleaser will look for the staged files
    repositories {
        maven {
            url = layout.buildDirectory.dir('staging-deploy')
        }
    }
}

jreleaser {
    signing {
        active = 'ALWAYS'
        armored = true
        verify = false
    }
    deploy {
        maven {
            mavenCentral {
                'release-deploy' {
                    active = 'RELEASE'
                    url = 'https://central.sonatype.com/api/v1/publisher'
                    applyMavenCentralRules = true
                    stagingRepository('build/staging-deploy')
                }
            }
            nexus2 {
                'snapshot-deploy' {
                    active = 'SNAPSHOT'
                    snapshotUrl = 'https://central.sonatype.com/repository/maven-snapshots/'
                    applyMavenCentralRules = true
                    snapshotSupported = true
                    closeRepository = true
                    releaseRepository = true
                    stagingRepository('build/staging-deploy')
                }
            }
        }
    }
}

javadoc {
    // suppress warnings
    options.addStringOption('Xdoclint:none', '-quiet')

    if(JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}
